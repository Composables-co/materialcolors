apply plugin: 'maven-publish'
apply plugin: 'signing'

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    if (project.plugins.findPlugin('com.android.library')) {
        // For Android libraries
        from android.sourceSets.main.java.srcDirs
        from android.sourceSets.main.kotlin.srcDirs
    } else {
        // For pure Kotlin libraries, in case you have them
        from sourceSets.main.java.srcDirs
        from sourceSets.main.kotlin.srcDirs
    }
}

artifacts {
    archives androidSourcesJar
}

group = PUBLISH_GROUP_ID
version = PUBLISH_VERSION

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId PUBLISH_GROUP_ID
                artifactId PUBLISH_ARTIFACT_ID
                version PUBLISH_VERSION

                if (project.plugins.findPlugin('com.android.library')) {
                    from components.release
                } else {
                    from components.java
                }

                artifact androidSourcesJar

                pom {
                    name = PUBLISH_ARTIFACT_ID
                    description = LIB_DESCRIPTION
                    url = 'https://' + GITHUB_URL
                    licenses {
                        license {
                            name = 'ContactStore License'
                            url = 'https://' + GITHUB_URL + '/blob/main/LICENSE'
                        }
                    }
                    developers {
                        developer {
                            id = 'composables-co'
                            name = 'Composables'
                            email = 'alex@composables.com'
                        }
                    }

                    scm {
                        connection = 'scm:git:' + GITHUB_URL
                        developerConnection = 'scm:git:ssh://' + GITHUB_URL
                        url = 'https://' + GITHUB_URL + '/tree/main'
                    }
                }
            }
        }
    }
}

signing {
    useInMemoryPgpKeys(
            rootProject.ext["signingKeyId"],
            rootProject.ext["signingKey"],
            rootProject.ext["signingPassword"],
    )
    sign publishing.publications
}